// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    
    Mux16(a=x, b=false, sel=zx, out=outZX);
    Not16(in=outZX, out=notOutZX);
    Mux16(a=outZX, b=notOutZX, sel=nx, out=outZNX);

    Mux16(a=y, b=false, sel=zy, out=outZY);
    Not16(in=outZY, out=notOutZY);
    Mux16(a=outZY, b=notOutZY, sel=ny, out=outZNY);

    And16(a=outZNX, b=outZNY, out=andXY);
    Add16(a=outZNX, b=outZNY, out=addXY);

    Mux16(a=andXY, b=addXY, sel=f, out=addOrAndXY);
    
    Not16(in=addOrAndXY, out=notAddOrAndXY);
    Mux16(a=addOrAndXY, b=notAddOrAndXY, sel=no, out[0..7]=outInternal1, out[8..15]=outInternal2, out=out, out[15]=lsb);

    Or8Way(in=outInternal1, out=orOne);
    Or8Way(in=outInternal2, out=orTwo);
    Or(a=orOne, b=orTwo, out=anyOutTrue);
    Not(in=anyOutTrue, out=zr);

    Mux(a=false, b=true, sel=lsb, out=ng);

}